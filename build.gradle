wrapper {
    gradleVersion = '5.2.1'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = pGroup
version = pVersion

if (project.hasProperty("topobyte")) {
    apply from: 'file:///' + System.getProperty("user.home") \
        + '/.gradle/topobyte.gradle'
}

repositories {
    maven {
        url 'http://mvn.topobyte.de'
    }
    maven {
        url 'http://mvn.slimjars.com'
    }
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'de.topobyte:adt-geo:0.0.4'
    compile 'commons-codec:commons-codec:1.12'
    compile 'org.slf4j:slf4j-api:1.6.1'

    testRuntime 'org.slf4j:slf4j-log4j12:1.6.1'
    testRuntime 'log4j:log4j:1.2.16'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

def pomInfo = new XmlSlurper().parse(new File('info.pom'));

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    name pomInfo.name
                    description pomInfo.description
                    url pomInfo.url
                }
            }
        }
    }
}
